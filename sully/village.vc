event /* 0 */
{
 flags[151]=0;
 EnforceAnimation();
 CallEvent(36,46,51,81);
 CallEvent(36,47,55,81);
 CallEvent(36,48,51,85);
 CallEvent(36,49,55,85);
 CallEvent(36,27,53,83);
 if(flags[44])
 {
  AlterBTile(53,59,157,2);
  AlterBTile(54,59,158,2);
  AlterBTile(55,59,159,2);
  AlterBTile(53,60,155,2);
  AlterBTile(54,60,195,0);
  AlterBTile(55,60,156,2);
 }
 if(flags[106])
 {
  Redraw();
  FadeIn(30);
  Fade=0;
  SetFace(char(1),1);
  SetFace(char(3),1);
  flags[106]=0;
  ChainEvent(12);
 }
 if(flags[128])
 {
  AlterBTile(13,6,89,2);
 }
}

event /* 1 */
{
 mapswitch("TEST.MAP",25,74,0);
}

event /* 2 */
{
 mapswitch("TEST.MAP",32,72,0);
}

event /* 3 */
{
 stext(0,"Welcome to the town of Rodne.",
         "Yes, I'm the dork who says the",
         "town name and proud of it!");
}

event /* 4 */
{
 if(flags[128])
 {
  stext(0,"I... I don't believe it!",
          "Please use the power of the",
          "carrot to destroy Lord Stan.");
  return;
 }
 stext(0,"Lord Stan has cursed our sky",
         "so we never have any rain.",
         "Only this carrot can grow.");
 stext(0,"We all pray that one day it",
         "will grow to be the Sacred",
         "Carrot of Ultimate Power.");
 if(char(3))
 {
  stext(3,"The chance that this root will",
          "grow into an omnipotent holy",
          "weapon is exceedingly remote.");
  stext(1,"Don't be such an egghead, Dex.","","");
 }
}

event /* 5 */
{
 stext(0,"If it's a technician you're",
         "looking for, the lab is in the",
         "southwest area of the village.");
 stext(0,"Sara doesn't understand that",
         "we live in simple happiness",
         "without evil technology.");
 if(char(2))
 {
  stext(2,"Hey!","","");
 }
}

event /* 6 */
{
 stext(0,"This is the elder's place. He",
         "will teach you about the",
         "heritage of our village.");
 if(char(3))
 {
  stext(3,"Oh, boy! Learning!","","");
 }
}

event /* 7 */
{
 stext(0,"We live in fear of both Lord",
         "Stan and Big Daddy Bubba. Lord",
         "Stan resides in Castle Heck.");
 stext(0,"Castle Heck is the treacherous",
         "looking place on the shores of",
         "the eastern peninsula.");
 stext(0,"Big Daddy Bubba is a reclusive",
         "hermit who captures people for",
         "his love shack in the Forest.");
 if(char(2))
 {
  stext(2,"Yes, Bubba is a big stinker.",
          "We shall also beat Lord Stan",
          "and his evil lackey, Galfrey!");
  if(char(5))
  {
   stext(5,"Hey, that hurts...","","");
  }
 }
}

event /* 8 */
{
 stext(0,"There's a trail leading deep",
         "into the Forest on the east",
         "edge of the village.");
 stext(0,"If you go, be sure to steer",
         "clear of Big Daddy Bubba's",
         "love shack.");
 if(char(2) and char(4))
 {
  stext(2,"Love shack... I bet you and",
          "Darin plan on spending some",
          "time there, eh, Crystal?");
  stext(4,"Don't make me scratch your",
          "eyes out.","");
 }
}

event /* 9 */
{
 if(flags[128])
 {
  stext(0,"You... monster! You absolute",
          "monster! How you could you rob",
          "me of my sole passion in life?");
  return;
 }
 stext(0,"My sole purpose in life is to",
         "eat that sacred carrot, but",
         "that goober keeps guarding it.");
 stext(0,"Sooner or later, he will grow",
         "sleepy. I shall bide my time,",
         "then I shall strike!");
 if(char(4))
 {
  stext(4,"What an adorable little",
          "rabbit. I shall pet it.","");
  if(char(2) and char(5))
  {
   stext(2,"Do you think rabbits are",
           "cute, Galfrey?","");
   stext(5,"Not really.","","");
  }
 }
}

event /* 10 */
{
 stext(0,"This trail leads deep into the",
         "Forest. Enjoy the serene",
         "beauty of the wind and trees.");
 stext(0,"Umm... er... I mean...",
         "*CHIRP*!","");
 if(char(3))
 {
  stext(3,"What a remarkable bird.","","");
 }
}

event /* 11 */
{
 stext(0,"The village elder is upstairs.",
         "He remembers the early days of",
         "this crappy little village.");
 if(char(2))
 {
  stext(2,"I'd really rather you not know",
          "about this, Darin.","");
  if(char(4))
  {
   stext(4,"What's the deal, Darin? Juicy",
           "secrets about Sara's sordid",
           "past? I must know!");
  }
 }
}

event /* 12 */
{
 if(char(2))
 {
  stext(0,"Good luck, young folks. Enjoy",
          "your stay here in Rodne.","");
 }
 if(flags[32])
 {
  stext(0,"Good luck, young folks. Enjoy",
          "your stay here in Rodne.","");
  return;
 }
 if(!flags[114] AND !char(2))
 {
  stext(0,"I am the village elder. Have",
          "a seat and I will tell you",
          "about the ancestry of Rodne.");
  stext(0,"It all began when a young",
          "engineer named Sara started a",
          "laboratory in the Forest.");
  stext(0,"Her machine to clone rats",
          "exploded and created all the",
          "clone people you see here.");
  MapPaletteGradient(240,247,1,0);
  DisableMenu();
  DisableSave();
  RemoveCharacter(1);
  RemoveCharacter(3);
  AddCharacter(2);
  Flags[106]=1;
  Mapswitch("OLDVILLE.MAP",40,10,0);
 }
 stext(0,"So you see, it's not because",
         "of a lazy entity artist, but",
         "rather it's scientific.");
 stext(0,"Sara still lives here,",
         "designing her machines and",
         "making the world spiffier.");
 flags[32]=1;
}

event /* 13 */
{
 stext(0,"I am Thor, the blacksmith.",
         "Well met, travelers! What",
         "can I do for you today?");
 Shop(14,18,5,25,13,8,28,11,15,19,16,20);
 stext(0,"I thank you, sir! Good day.","","");
}

event /* 14 */
{
 stext(0,"You look like a nice bunch of",
         "kids. I'll show you the real",
         "medicine, not just Tic-Tacs.");
 Shop(2,38,39,1,41,43);
 stext(0,"Aww... come on! Buy one more",
         "medicine, please?","");
}

event /* 15 */
{
 if(flags[33])
 {
  stext(0,"Seeya, guys! Have fun",
          "defeating Lord Stan!","");
  if(char(2))
  {
   stext(2,"I'll be back soon, Slasher,",
           "and I'll be sure to pick up",
           "some sunflower seeds.");
  }
  return;
 }
 if(!flags[34])
 {
  stext(0,"Squeak! Squeak!","","");
  stext(1,"How terrible! They're",
          "enslaving and experimenting",
          "on poor helpless animals!");
  stext(0,"No, actually Sara treats me",
          "well, except I don't have any",
          "friends.");
  stext(0,"After Sara's machine blew up,",
          "she was unable to clone any",
          "more cute little rats.");
  stext(3,"Sara? Yes, we need to speak",
          "to her. Where is she?","");
  stext(0,"She went into the Forest to",
          "find some wood for her new",
          "machine.");
  stext(1,"Well, we can't wait for her",
          "to get back. Dexter, let's go",
          "into the Forest to find her.");
  stext(3,"Agreed. Thank you, cute",
          "little rat.","");
 }
 flags[33]=1;
}

event /* 16 */
{
 warp(8,67,0);
 Banner("Elder's Home",3);
}

event /* 17 */
{
 warp(30,69,0);
 Banner("Blacksmith",3);
}

event /* 18 */
{
 warp(34,90,0);
 Banner("Pharmacy",3);
}

event /* 19 */
{
 warp(53,69,0);
 Banner("Laboratory",3);
}

event /* 20 */
{
 warp(11,21,0);
}

event /* 21 */
{
 warp(11,79,0);
 Banner("2F",3);
}

event /* 22 */
{
 Fade=1;
 warp(13,61,0);
}

event /* 23 */
{
 warp(73,64,0);
}

event /* 24 */
{
 warp(53,80,0);
 Banner("Basement",3);
}

event /* 25 */
{
 if(!flags[44] AND char(2))
 {
  stext(2,"This leads to my basement.",
          "Let me remove the dust that",
          "conceals the passage.");
  AlterBTile(53,59,157,2);
  AlterBTile(54,59,158,2);
  AlterBTile(55,59,159,2);
  AlterBTile(53,60,155,2);
  AlterBTile(54,60,195,0);
  AlterBTile(55,60,156,2);
  stext(2,"Please feel free to enter.",
          "The Activator is in my",
          "storage room below.");
  flags[44]=1;
  return;
 }
 if(flags[44])
 {
  warp(73,68,0);
 }
}

event /* 26 */
{
 warp(54,61,0);
}

event /* 27 */
{
 warp(33,21,0);
}

event /* 28 */
{
 warp(29,33,0);
}

event /* 29 */
{
 warp(7,35,0);
}

event /* 30 */
{
 ChainEvent(35,46,51,81,38);
}

event /* 31 */
{
 ChainEvent(35,47,55,81,40);
}

event /* 32 */
{
 ChainEvent(35,48,51,85,27);
}

event /* 33 */
{
// ChainEvent(35,49,55,85,20);
 if(!flags[49])
 {
  AlterBTile(55,85,177,2);
  soundeffect(3);
  Banner("Found 450 GP!",3);
  GiveGP(450);
  flags[49]=1;
 }

}

event /* 34 */
{
 ChainEvent(35,27,53,83,22);
}

event /* 35 */                                  // Master Chest Script
{
 if(!flags[var(0)])
 {
  AlterBTile(var(1),var(2),177,2);
  soundeffect(3);
  giveitem(var(3));
  flags[var(0)]=1;
 }
}

event /* 36 */                                  // Master Chest Auto-Exec
{
 if(flags[var(0)])
 {
  AlterBTile(var(1),var(2),177,2);
 }
}

event /* 37 */
{
if(!flags[105])
{
 flags[105]=32;
}
 SoundEffect(10);
 Timer=0;
 While(Timer<100)
 {
  PaletteMorph(20,20,63,100-Timer,63);
 }
 HealAll();
 stext(0,"HP/MP/Status restored!","","");
}

event /* 38 */
{
 if(!flags[150])
 {
  soundeffect(8);
 }
 FakeBattle();
}

event /* 39 */
{
 if(item(24) AND !flags[128])
 {
  stext(1,"This is strange... the Pearl",
          "of Truth is glowing brighter",
          "when I hold it down here.");
  Wait(1);
  Timer=0;
  While(Timer<100)
  {
   PaletteMorph(63,63,63,Timer,63);
  }
  AlterBTile(13,6,89,2);
  Redraw();
  Timer=0;
  While(Timer<100)
  {
   PaletteMorph(63,63,63,100-Timer,63);
  }
  stext(1,"Wow! The Pearl has lifted the",
          "Sacred Carrot of Ultimate",
          "Power from the earth!");
  SoundEffect(10);
  GiveItem(45);
  flags[128]=1;
 }
}


event /* 40 */
{
 stext(1,"What is this monstrosity?",
         "It looks like a mangled wreck",
         "of pipes.");
 if(char(2))
 {
  stext(2,"I'm *trying* to re-build it,",
          "thank you very much. That's",
          "why I needed the wood.");
 }
}
